bootJar {
    enabled = false
}

jar {
    enabled = true
}

// Copy the resources referred by the Doma annotation processors to
// the destinationDir of the compileJava task
task copyDomaResources(type: Sync)  {
    from sourceSets.main.resources.srcDirs
    into compileJava.destinationDir
    include "doma.compile.config"
    include "META-INF/**/*.sql"
    include "META-INF/**/*.script"
}

compileJava {
    // Depend on the above task
    dependsOn copyDomaResources
    options.encoding = "UTF-8"
}

compileTestJava {
    options.encoding = "UTF-8"
    // Disable the annotation processors during the test run
    options.compilerArgs = ["-proc:none"]
}

dependencies {
    compile project(":sample-common")

    // springframework
    compile "org.springframework.boot:spring-boot-starter-aop"
    compile "org.springframework.boot:spring-boot-starter-validation"
    compile "org.springframework.boot:spring-boot-starter-mail"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    compile "org.springframework.boot:spring-boot-starter-jdbc"
    compile "org.springframework.boot:spring-boot-starter-json"

    // 도마에서 스프링 프레임워크 배제하기
    annotationProcessor "org.seasar.doma.boot:doma-spring-boot-starter:1.1.1"
    compile("org.seasar.doma.boot:doma-spring-boot-starter:1.1.1") {
        exclude group: "org.springframework.boot"
    }

    // thymeleaf
    compile "org.codehaus.groovy:groovy:${groovyVersion}"
    compile("nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.3.0") {
        exclude group: "org.codehaus.groovy", module: "groovy"
        exclude group: "org.thymeleaf", module: "thymeleaf"
    }

    // jackson
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-csv"

    // modelmapper
    compile "org.modelmapper:modelmapper:0.7.5"

    // mysql database
    compile "mysql:mysql-connector-java"
    compile "org.flywaydb:flyway-core"

    // API 문서 자동 생성을 이용하기 위해 추가하는 라이브러리
    compile "io.springfox:springfox-swagger2:2.6.0"
    // Swagger UI를 이용하기 위해 추가하는 라이브러리
    compile "io.springfox:springfox-swagger-ui:2.6.0"

    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.spockframework:spock-spring:${spockVersion}"
}
